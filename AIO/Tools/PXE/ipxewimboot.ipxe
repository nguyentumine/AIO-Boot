#!ipxe
console --x 1024 --y 768
console --picture ${boot-url}/AIO/grub/themes/splash.png

# Setup some basic convenience variables
set menu-timeout 5000
set submenu-timeout ${menu-timeout}

# Ensure we have menu-default set to something
isset ${menu-default} || set menu-default wimboot

######## MAIN MENU ###################
:start
menu Welcome to iPXE's Boot Menu
item
item --gap -- ------------------------------ Utilities --------------------------------
item --key 1 wimboot	[1] WinPE & Setup (${wimfile})
item --gap -- ------------------------------ Advanced ---------------------------------
item --key c config		[c] Configure settings
item --key s shell		[s] Enter iPXE shell
item --key r reboot		[r] Reboot
item --key p poweroff	[p] Power Off
item --key e exit		[e] Exit (boot local disk)
choose --default wimboot --timeout 30000 target && goto ${target}

########## UTILITY ITEMS ####################
:shell
echo Type exit to get the back to the menu
shell
set menu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:poweroff
poweroff

:exit
exit

:cancel
echo You cancelled the menu, dropping you to a shell
 
:config
config
goto start
 
:back
set submenu-timeout 0
clear submenu-default
goto start

:missingfile
echo 
echo Missing the file, please copy the boot.wim file to ${wimfile}. Dropping to shell.
echo 
goto shell

:wimboot
initrd ${boot-url}${wimfile}											boot.wim || goto missingfile
kernel ${boot-url}/AIO/Tools/MS/PXE/${arch2}/wimboot					rawbcd
isset ${legacy} && initrd ${boot-url}/AIO/Tools/MS/PXE/bootmgr			bootmgr || ###
isset ${legacy} && initrd ${boot-url}/AIO/Tools/MS/PXE/bootmgr.exe		bootmgr.exe || initrd ${boot-url}/AIO/Tools/MS/PXE/${arch2}/bootmgr.efi
isset ${legacy} && initrd ${boot-url}/AIO/Tools/MS/PXE/bcd				BCD || initrd ${boot-url}/AIO/Tools/MS/PXE/bcdefi			BCD
initrd ${boot-url}/AIO/Tools/MS/boot.sdi								boot.sdi
boot || goto failed
goto start